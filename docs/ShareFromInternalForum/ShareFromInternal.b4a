Build1=Default,xx.b4x.sharefrominternal
File1=MainLO.bal
FileGroup1=Default Group
Group=Default Group
Library1=contentresolver
Library2=core
Library3=javaobject
Library4=xui
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: https://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="5" android:targetSdkVersion="32"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~'End of default text.~\n~~\n~AddApplicationText(~\n~  <provider~\n~  android:name="android.support.v4.content.FileProvider"~\n~  android:authorities="$PACKAGE$.provider"~\n~  android:exported="false"~\n~  android:grantUriPermissions="true">~\n~  <meta-data~\n~  android:name="android.support.FILE_PROVIDER_PATHS"~\n~  android:resource="@xml/provider_paths"/>~\n~  </provider>~\n~)~\n~~\n~~\n~CreateResource(xml, provider_paths,~\n~   <files-path name="name" path="shared" />~\n~)
NumberOfFiles=1
NumberOfLibraries=4
NumberOfModules=0
Version=13.4
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: ShareFromInternal
	#VersionCode: 1
	#VersionName: V1.0
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: portrait
	#CanInstallToExternalStorage: False
	#BridgeLogger: True
#End Region

#Region  Activity Attributes 
	#FullScreen: true
	#IncludeTitle: false
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	
	Private xui As XUI
	
End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
	'These variables can only be accessed from this module.
	
	
	Dim SharedFolder As String
	
	Private ShareSingleBTN As Button
	Private ShowFileBTN As Button
	Private ShareMultipleFilesBTN As Button
End Sub

Sub Activity_Create(FirstTime As Boolean)
	'Do not forget to load the layout file created with the visual designer. For example:
	Activity.LoadLayout("MainLO")
	
	SharedFolder=File.DirInternal & "/shared"
	
	If File.Exists(SharedFolder,"") = False Then 
		File.MakeDir(SharedFolder,"")
	End If
	
	For i=1 To 4
		File.WriteString(SharedFolder,i & ".txt","This is file " & i)
	Next
	
	
End Sub


Sub ShareMultipleFiles
 
	Dim i As Intent
	i.Initialize("android.intent.action.SEND_MULTIPLE", "")
	i.SetType("text/plain")
 
	Dim Uris As List
	Uris.Initialize
 
	Dim Files As List=File.ListFiles(SharedFolder)
	For Each pdffile As String In Files
		Dim u As Uri = GetFileUri(pdffile)
		Uris.Add(u)
	Next
 
	Dim jo As JavaObject = i
	jo.RunMethod("putParcelableArrayListExtra", Array As Object("android.intent.extra.STREAM", Uris))
	i.Flags = 1
 
	StartActivity(i)
 
End Sub

Sub ShowFile (FileName As String) 'always under the shared folder!
	Dim MyIntent As Intent
	MyIntent.Initialize(MyIntent.ACTION_VIEW, GetFileUri(FileName))
	'MyIntent.SetType("application/pdf")
	MyIntent.SetType("text/plain")
	
	MyIntent.Flags = 1
		
	Try
		StartActivity(MyIntent)
	Catch
		MsgboxAsync("Sorry. No App available which supports the file type...","Error")
	End Try
	
End Sub

Sub ShareFile(FileName As String)
	Dim in As Intent
	in.Initialize(in.ACTION_SEND, "")
	'in.SetType("application/pdf")
	in.SetType("text/plain")
	in.PutExtra("android.intent.extra.STREAM", GetFileUri(FileName))
	in.Flags = 1 'FLAG_GRANT_READ_URI_PERMISSION
	StartActivity(in)
End Sub

Public Sub GetFileUri (FileName As String) As Object
	
		Dim f As JavaObject
		f.InitializeNewInstance("java.io.File", Array(SharedFolder, FileName))
		Dim fp As JavaObject
		Dim context As JavaObject
		context.InitializeContext
		fp.InitializeStatic("android.support.v4.content.FileProvider")
		Return fp.RunMethod("getUriForFile", Array(context, Application.PackageName & ".provider", f))
End Sub

'Replaces the intent Data field with the file uri.
'Resets the type field. Make sure to call Intent.SetType after calling this method
Public Sub SetFileUriAsIntentData (Intent As Intent, FileName As String)
	Dim jo As JavaObject = Intent
	jo.RunMethod("setData", Array(GetFileUri(FileName)))
	Intent.Flags = Bit.Or(Intent.Flags, 1) 'FLAG_GRANT_READ_URI_PERMISSION
End Sub

Sub Activity_Resume
	
	

End Sub



Sub Activity_Pause (UserClosed As Boolean)

End Sub





Private Sub ShareSingleBTN_Click
	ShareFile("1.txt")
End Sub

Private Sub ShowFileBTN_Click
	ShowFile("1.txt")
End Sub

Private Sub ShareMultipleFilesBTN_Click
	ShareMultipleFiles 'all in the shared folder
End Sub